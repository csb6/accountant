cmake_minimum_required(VERSION 3.25)

if(libofx_FOUND)
    add_executable(ofx_demo ofx_demo.cpp)
    target_compile_features(ofx_demo PUBLIC cxx_std_20)
    target_link_libraries(ofx_demo PUBLIC PkgConfig::libofx)
endif()

qt_standard_project_setup()

qt_add_library(util OBJECT util/sql_helpers.cpp)
target_include_directories(util PUBLIC util ${CMAKE_CURRENT_SOURCE_DIR})
target_precompile_headers(util PRIVATE <qobject.h>)
target_compile_features(util PUBLIC cxx_std_20)
target_link_libraries(util PUBLIC Qt6::Sql)

qt_add_library(qaccountant_models STATIC models/AccountTree.cpp models/AccountTransactions.cpp models/DatabaseManager.cpp)
target_include_directories(qaccountant_models PUBLIC models ${CMAKE_CURRENT_SOURCE_DIR})
target_compile_features(qaccountant_models PUBLIC cxx_std_20)
target_link_libraries(qaccountant_models PUBLIC Qt6::Core Qt6::Gui Qt6::Sql "util")
target_precompile_headers(qaccountant_models REUSE_FROM util)

set_property(SOURCE "${CMAKE_CURRENT_BINARY_DIR}/about.md" PROPERTY QT_RESOURCE_ALIAS "about.md") # Generated by generate_about_text
set_property(SOURCE ${CMAKE_SOURCE_DIR}/schemas/1-schema.sql PROPERTY QT_RESOURCE_ALIAS "schemas/1-schema.sql")

qt_add_library(qaccountant_resources STATIC)
target_compile_features(qaccountant_resources PUBLIC cxx_std_20)
qt_add_resources(qaccountant_resources "resources"
    PREFIX "qaccountant" BIG_RESOURCES
    FILES "${CMAKE_CURRENT_BINARY_DIR}/about.md" ${CMAKE_SOURCE_DIR}/schemas/1-schema.sql)

qt_add_executable(generate_about_text "${CMAKE_SOURCE_DIR}/tools/generate_about_text.cpp" "${CMAKE_SOURCE_DIR}/tools/spdx_parser.cpp")
target_include_directories(generate_about_text PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_compile_features(generate_about_text PUBLIC cxx_std_20)
target_link_libraries(generate_about_text PUBLIC Qt6::Core)
target_precompile_headers(generate_about_text REUSE_FROM util)
if("${Qt6_VERSION_MAJOR}" EQUAL 6 AND "${Qt6_VERSION_MINOR}" GREATER_EQUAL 8)
    target_compile_definitions(generate_about_text PRIVATE SPDX_PROVIDED)
    set(SBOM_PATH "${QT6_INSTALL_PREFIX}/share/qt/sbom/qtbase-${Qt6_VERSION}.spdx")
else()
    message(NOTICE "NOTE: SBOM generation disabled since your version of Qt (${Qt6_VERSION}) does not include SPDX files")
    set(SBOM_PATH ".")
endif()

add_custom_command(
    OUTPUT "about.md"
    COMMAND generate_about_text "${SBOM_PATH}" "about.md"
    DEPENDS generate_about_text
)

qt_add_executable(qaccountant
    main.cpp
    views/MainWindow.ui views/MainWindow.cpp
    views/AccountsView.ui views/AccountsView.cpp
    views/TransactionsView.ui views/TransactionsView.cpp
    views/AboutDialog.ui views/AboutDialog.cpp
    views/SecurityEditor.ui views/SecurityEditor.cpp
    views/NewAccountDialog.ui views/NewAccountDialog.cpp)
target_include_directories(qaccountant PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_compile_features(qaccountant PUBLIC cxx_std_20)
if(WITH_COMPILE_TIME_TRACE)
    target_compile_options(qaccountant PUBLIC -ftime-trace=compile_time_report)
endif()
target_link_libraries(qaccountant PUBLIC Qt6::Core Qt6::Sql Qt6::Widgets util qaccountant_models qaccountant_resources)
target_precompile_headers(qaccountant REUSE_FROM util)

if(SQL_QUERY_LOGGING)
    target_compile_definitions(qaccountant PRIVATE SQL_QUERY_LOGGING)
    target_link_libraries(qaccountant PUBLIC SQLite::SQLite3)
endif()

if(APPLE AND BUILD_APP_BUNDLE)
    set_target_properties(qaccountant PROPERTIES MACOSX_BUNDLE ON)
    set(RESOURCE_DIR "${CMAKE_SOURCE_DIR}/resources")
    set(ICON_DIR "${RESOURCE_DIR}/icon.iconset")
    set(ICNS_FILENAME "icon.icns")
    set(ICNS_PATH "${CMAKE_CURRENT_BINARY_DIR}/${ICNS_FILENAME}")
    add_custom_command(
        OUTPUT "${ICNS_PATH}"
        COMMAND iconutil -o "${ICNS_PATH}" -c icns "${ICON_DIR}"
        DEPENDS ${ICON_DIR}/icon_16x16.png ${ICON_DIR}/icon_32x32.png ${ICON_DIR}/icon_64x64.png
                ${ICON_DIR}/icon_128x128.png ${ICON_DIR}/icon_256x256.png ${ICON_DIR}/icon_512x512.png)
    set(MACOSX_BUNDLE_ICON_FILE "${ICNS_FILENAME}")
    set_source_files_properties("${ICNS_PATH}" PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")
    target_sources(qaccountant PRIVATE "${ICNS_PATH}")

    find_program(MACDEPLOYQT_EXECUTABLE macdeployqt HINTS "${QT6_INSTALL_PREFIX}/${QT6_INSTALL_BINS}")
    # Fixup the .app bundle to include/use all of the shared libraries needed by Qt
    # Seem to need to run this twice for some reason (fails with error on first run)
    add_custom_target(deploy
        COMMAND "${MACDEPLOYQT_EXECUTABLE}" "${CMAKE_CURRENT_BINARY_DIR}/QAccountant.app" "-always-overwrite"
        DEPENDS qaccountant)
    set_target_properties(qaccountant PROPERTIES
        ADDITIONAL_CLEAN_FILES "${CMAKE_CURRENT_BINARY_DIR}/QAccountant.app"
    )
endif()
